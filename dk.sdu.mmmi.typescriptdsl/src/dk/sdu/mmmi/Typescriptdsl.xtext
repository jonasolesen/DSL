grammar dk.sdu.mmmi.Typescriptdsl with org.eclipse.xtext.common.Terminals

generate typescriptdsl "http://www.sdu.dk/mmmi/Typescriptdsl"

Database:
	'database' name=ID
	
	config += Config*
	
	tables += Table+
;

Config returns Config:
	{ConfigProvider} 'provider' value=STRING |
	{ConfigUrl} 'url' value=STRING |
	{ConfigPort} 'port' value=INT |
	{ConfigUsername} 'username' value=STRING |
	{ConfigPassword} 'password' value=STRING
;

Table:
	'table' name=ID ('extends' superType=[Table])? ':'
		attributes+=Attribute+
		
		functions+=Function*
;

Attribute:
	name=ID ':' type=AttributeType (optional?='?')? (primary?='primary')? 
		('constraint' constraint=OrConstraint)?
;

Function:
	name=ID ':' function=(FunctionRead | FunctionCreate | FunctionUpdate | FunctionDelete)
;

FunctionRead:
	'read' where=FunctionReadParameters (select=FunctionSelect)?
;

FunctionCreate:
	'create' where=FunctionWriteParameters (select=FunctionSelect)?
;

FunctionUpdate:
	'update' where=FunctionReadParameters 'set' update=FunctionWriteParameters (select=FunctionSelect)?
;

FunctionDelete:
	'delete' where=FunctionReadParameters (select=FunctionSelect)?
;

FunctionReadParameters:
	'(' parameters += ParameterOrConstraint (',' parameters += ParameterOrConstraint)* ')'
;

FunctionWriteParameters:
	'(' parameters += Record (',' parameters += Record)* ')'
;

Record:
	key=[Attribute] ':' value=SimpleType
;

Boolean:
	'true' | 'false'
;

SimpleType:
    INT | STRING | Boolean
;

FunctionSelect:
	{FunctionSelect} 'select' props += [Attribute]+
;

ParameterOrConstraint:
	constraint = OrConstraint | parameter = [Attribute]
;

OrConstraint returns Constraint:
	AndConstraint ('||' {Or.left=current} right=AndConstraint)* 
;

AndConstraint returns Constraint:
	Condition ('&&' {And.left=current} right=Condition)*
;

Condition returns Constraint:
	Comparison |
	{RegexConstraint} value=STRING |
 	{StringConstraint} left=[Attribute] ('contains' | 'equals') right=STRING
;

Comparison:
	left=Expression operator=Operator right=Expression
;

Expression returns Expression:
	Factor ( ('+' {Add.left=current} | '-' {Sub.left=current}) right=Factor)*
;

Factor returns Expression:
	Primitive ( ('*' {Mul.left=current} | '/' {Div.left=current}) right=Primitive)*
;

Primitive returns Expression:
	Field | NumberExp | Parenthesis
;

Operator: 
	{Gt} '>' | {Gte} '>=' | {Lt} '<' | {Lte} '<=' | {Equals} '=='
;

Field:
	attribute=[Attribute]
;

Parenthesis:
	'(' exp=Expression ')'
;

NumberExp:
	value=INT
;

AttributeType returns AttributeType:
	PrimaryType | TableType
;

PrimaryType returns PrimaryType:
	{IntType} 'int' | {StringType} 'string' | {DateType} 'date'
;

TableType returns TableType:
	table=[Table]
;