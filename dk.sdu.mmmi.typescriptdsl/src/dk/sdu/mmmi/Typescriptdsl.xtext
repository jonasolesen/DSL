grammar dk.sdu.mmmi.Typescriptdsl with org.eclipse.xtext.common.Terminals

generate typescriptdsl "http://www.sdu.dk/mmmi/Typescriptdsl"

Database:
	'database' name=ID
	
	config += Config*
	
	tables += Table+
;

Config returns Config:
	{ConfigProvider} 'provider' value=STRING |
	{ConfigUrl} 'url' value=STRING |
	{ConfigPort} 'port' value=INT |
	{ConfigUsername} 'username' value=STRING |
	{ConfigPassword} 'password' value=STRING
;

Table:
	'table' name=ID ('extends' superType=[Table])? ':'
		attributes+=Attribute+
		
		functions+=BaseFunction*
;

Attribute:
	name=ID ':' type=AttributeType (optional?='?')? (primary?='primary')? 
		('constraint' constraint=OrConstraint)?
;

BaseFunction:
	name=ID ':' body=(FunctionRead | FunctionCreate | FunctionUpdate | FunctionDelete)
;

FunctionCreate returns Function:
	{FunctionCreate} 'create' data=FunctionWriteParameters (select=FunctionSelect)?
;

FunctionRead returns Function:
	{FunctionRead} 'read' where=FunctionReadParameters (select=FunctionSelect)?
;

FunctionUpdate returns Function:
	{FunctionUpdate} 'update' where=FunctionReadParameters 'set' update=FunctionWriteParameters (select=FunctionSelect)?
;

FunctionDelete returns Function:
	{FunctionDelete} 'delete' where=FunctionReadParameters (select=FunctionSelect)?
;

FunctionReadParameters:
	'(' parameters+=ParameterOrConstraint (',' parameters+=ParameterOrConstraint)* ')'
;

FunctionWriteParameters:
	'(' records+=Record (',' records+=Record)* ')'
;

Record:
	key=[Attribute] ':' value=SimpleType
;

Boolean:
	'true' | 'false'
;

SimpleType:
    {SimpleType} INT | {SimpleType} STRING | {BoolType} Boolean
;

FunctionSelect:
	'select' props+=[Attribute]+
;

ParameterOrConstraint:
	constraint = OrConstraint | attribute=[Attribute]
;

OrConstraint returns Constraint:
	AndConstraint ('||' {Or.left=current} right=AndConstraint)* 
;

AndConstraint returns Constraint:
	Condition ('&&' {And.left=current} right=Condition)*
;

Condition returns Constraint:
	Comparison |
	{RegexConstraint} value=STRING |
	{BooleanConstraint} left=[Attribute] 'is' right=Boolean |
	{StringConstraint} left=[Attribute] ('contains' | 'equals') right=STRING
;

Comparison:
	left=Expression operator=Operator right=Expression
;

Expression returns Expression:
	Factor ( ('+' {Add.left=current} | '-' {Sub.left=current}) right=Factor)*
;

Factor returns Expression:
	Primitive ( ('*' {Mul.left=current} | '/' {Div.left=current}) right=Primitive)*
;

Primitive returns Expression:
	Field | NumberExp | Parenthesis
;

Operator: 
	{Gt} '>' | {Gte} '>=' | {Lt} '<' | {Lte} '<=' | {Equals} '=='
;

Field:
	attribute=[Attribute]
;

Parenthesis:
	'(' exp=Expression ')'
;

NumberExp:
	value=INT
;

AttributeType returns AttributeType:
	PrimaryType | TableType
;

PrimaryType returns PrimaryType:
	{IntType} 'int' | {StringType} 'string' | {DateType} 'date' | {BoolType} 'boolean'
;

TableType returns TableType:
	table=[Table]
;